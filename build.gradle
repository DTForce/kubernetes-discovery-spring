buildscript {
	ext {
		springBootVersion = '2.1.4.RELEASE'
		springCloudVersion = 'Greenwich.SR1'
	}
    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven'
apply plugin: 'signing'

group "com.dtforce"
def artifactIdentifier = "kubernetes-discovery-spring"

// Thanks https://stackoverflow.com/a/35041457
def getGitRef = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'name-rev', '--name-only', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitCommit = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def versionFromEnv = System.env["BUILD_VERSION"]
version = (versionFromEnv != null ? versionFromEnv : "${getGitRef}-${getGitCommit}-SNAPSHOT")

sourceCompatibility = 1.8

dependencyManagement {
	imports {
		mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
		mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
	}
    dependencies {
        dependency 'io.fabric8:kubernetes-client:4.2.2'
        dependency 'io.fabric8:kubernetes-server-mock:4.2.2'
        dependency 'org.assertj:assertj-core:3.9.0'
		dependency 'com.github.ben-manes.caffeine:caffeine:2.7.0'
    }
}

dependencies {
    // Spring + Spring Cloud
    api('org.springframework.boot:spring-boot-autoconfigure')
	api('org.springframework:spring-context')
	api('org.springframework.cloud:spring-cloud-commons')
	api('org.springframework.cloud:spring-cloud-starter-netflix-ribbon')

    // Kubernetes Client
	api('io.fabric8:kubernetes-client')

    // test dependencies
    testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('org.springframework:spring-web')
    testCompile('io.fabric8:kubernetes-server-mock') {
		exclude group: 'io.sundr', module: 'builder-annotations'
	}
    testCompile('org.assertj:assertj-core')
}

compileJava.dependsOn(processResources)

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives sourceJar, javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: System.env['MAVEN_REPO_URL']) {
                authentication(
                        userName: System.env['MAVEN_USER'],
                        password: System.env['MAVEN_PASSWORD']
                )
            }

            pom.artifactId = artifactIdentifier
            pom.project {
                name artifactIdentifier
                packaging 'jar'
                description 'Kubernetes Service Discovery for Spring Cloud and Ribbon'
                url 'https://github.com/DTForce/kubernetes-discovery-spring'

                scm {
                    url 'https://github.com/DTForce/kubernetes-discovery-spring'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'stephane.lepin'
                        name 'St√©phane Lepin'
                        email 'stephane.lepin@dtforce.com'
                    }
					developer {
						id 'jan.mares'
						name 'Jan Mares'
						email 'jan.mares@dtforce.com'
					}
                }
            }
        }
    }
}
